#!/usr/bin/perl
#===============================================================================
#
#         FILE: wd_fcgi.fpl
#
#  DESCRIPTION:  FastCGI script for WebDAO project
#       AUTHOR:  Aliaksandr P. Zahatski (Mn), <zag@cpan.org>
#===============================================================================
#$Id: wd_fcgi.fpl,v 1.1 2006/10/13 12:39:09 zag Exp $

use HTML::WebDAO;
use CGI::Fast qw(:standard);
use HTML::WebDAO::CVcgi;
use HTML::WebDAO::Session;
use HTML::WebDAO::Lex;
use Data::Dumper;
use strict;

sub _parse_str_to_hash {
    my $str = shift;
    return unless $str;
    my %hash = map { split( /=/, $_ ) } split( /;/, $str );
    foreach ( values %hash ) {
        s/^\s+//;
        s/\s+^//;
    }
    \%hash;
}

my ( $def_store_class, $def_session_class, $def_eng_class );
my ( $store_class, $session_class, $eng_class ) = map {
    eval "require $_"
      or die $@;
    $_
  } (
    $def_store_class   = $ENV{wdStore}   || 'HTML::WebDAO::Store::Abstract',
    $def_session_class = $ENV{wdSession} || 'HTML::WebDAO::Session',
    $def_eng_class     = $ENV{wdEngine}  || 'HTML::WebDAO::Engine'
  );
my $count_req = $ENV{wdFCGIreq} || -1;
while ( $count_req != 0 and my $fcgi_obj = new CGI::Fast ) {

    #customize ENV
    foreach my $rec (
        [
            \$store_class,
            $ENV{wdStore} || $def_store_class,
            $ENV{wdStore} eq $def_store_class
        ],
        [
            \$session_class,
            $ENV{wdSession} || $def_session_class,
            $ENV{wdSession} eq $def_session_class
        ],
        [
            \$eng_class,
            $ENV{wdEngine} || $def_eng_class,
            $ENV{wdEngine} eq $def_eng_class
        ]
      )
    {
        next unless defined $rec->[1];
        next if ${ $rec->[0] } eq $rec->[1] or $rec->[2];
        eval "require @{[$rec->[1]]}" or die $@;
        ${ $rec->[0] } = $rec->[1]

    }
    my $store_obj =
      $store_class->new( %{ &_parse_str_to_hash( $ENV{wdStorePar} ) || {} } );
    my $sess = $session_class->new(
        %{ &_parse_str_to_hash( $ENV{wdSessionPar} ) || {} },
        store => $store_obj,
        cv    => new HTML::WebDAO::CVcgi:: $fcgi_obj,

    );
    $sess->set_header( -type => 'text/html; charset=utf-8' );
    my ($filename) = grep { -r $_ && -f $_ } $ENV{wdIndexFile},
      "$ENV{DOCUMENT_ROOT}/$ENV{wdIndexFile}",
      "$ENV{DOCUMENT_ROOT}/index.xhtml";
    die
      "$0 ERR:: file not found or can't access (wdIndexFile): $ENV{wdIndexFile}"
      unless $filename;
    my $content = qq!<wD><include file="$filename"/></wD>!;
    my $lex     = new HTML::WebDAO::Lex:: content => $content;
    my $eng     = $eng_class->new(
        %{ &_parse_str_to_hash( $ENV{wdEnginePar} ) || {} },
        lexer    => $lex,
        register => { 'HTML::WebDAO::Comp::ListEnv' => 'listenv' },
        session  => $sess,
    );
    $sess->ExecEngine($eng);
    $sess->destroy;
    --$count_req if $count_req > 0;
}
sleep( rand(3) );
FCGI::finish();

__END__

=head1 NAME

wd_fcgi.fpl - FastCGI script for WebDAO project

=head1 SETUP

    -initial-env wdEnginePar=test=test;


=head1 SEE ALSO

http://sourceforge.net/projects/webdao, HTML::WebDAO

=head1 AUTHOR

Zahatski Aliaksandr, E<lt>zag@cpan.orgE<gt>

=head1 COPYRIGHT AND LICENSE

Copyright 2003-2007 by Zahatski Aliaksandr

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself. 

=cut
